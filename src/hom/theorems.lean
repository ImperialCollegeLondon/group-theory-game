import hom.quotient
import subgroup.lattice

local attribute [instance] classical.prop_decidable -- I hope we don't mind this

/-
  The type of group homs G â†’ H is
  denoted f : G â†’* H
  and the underlying function of types is â‡‘f : G â†’ H
  TODO: can we switch those arrows off?
  The axiom: if `f : G â†’* H` then `f.map_mul`
  is the assertion that for all a, b âˆˆ G
  we have f(a) = f(b). 
  Our first job is to prove `f.map_one` and `f.map_inv`.

  `f.map_one : f 1 = 1`
  `f.map_inv {x : G} : f (xâ»Â¹) = f(x)â»Â¹`

-/

-- the entire project takes place in the mygroup namespace
namespace mygroup

-- We're proving things about group homs so this all goes in the `group_hom`
-- namespace

open set mygroup.subgroup

namespace group_hom

variables {G H K : Type} [group G] [group H] [group K]

/-- If f is a group homomorphism then f 1 = 1. -/
@[simp] -- it's a good simp lemma
lemma map_one (f : G â†’* H) : f 1 = 1 :=
begin
  have h : f 1 * f 1 = f 1,
    rw â†f.map_mul,
    rw group.one_mul, -- annoying but stops cheating
    -- TODO: can I kill one_mul somehow? I asked on Zulip
  rw group.mul_left_eq_self at h, -- annoying
  assumption
end

/-- If f is a group homomorphism then f(xâ»Â¹) = f(x)â»Â¹ -/
@[simp] -- it's also a good simp lemma
lemma map_inv (f : G â†’* H) {x : G} : f (xâ»Â¹) = (f x)â»Â¹ :=
begin
  apply group.eq_inv_of_mul_eq_one,
  rw â†f.map_mul,
  rw group.mul_left_inv,
  rw f.map_one,
  -- refl
end

-- We prove the theorems here (only);
-- definitions need to go elsewhere

-- Rather than defining the kernel as the preimage of {1}, I think defining it 
-- as a subgroup of the domain is better

/-- The kernel of a homomorphism `f : G â†’* H` is the normal subgroup of `G` whos 
  carrier is the preimage of `{1}`, i.e. `f â»Â¹' {1}` -/
def kernel (f : G â†’* H) : normal G := 
{ carrier := f â»Â¹' {1},
  one_mem' := map_one _, 
  mul_mem' := 
    begin
      intros _ _ hx hy,
      rw [mem_preimage, mem_singleton_iff] at *,
      rw [map_mul f, hx, hy, group.mul_one]
    end,
  inv_mem' := 
    begin
      intros _ hx,
      rw [mem_preimage, mem_singleton_iff] at *,
      rw [map_inv f, hx, group.one_inv]
    end,
  conj_mem' :=
    begin
      intros _ hn _,
      rw [mem_preimage, mem_singleton_iff] at *,
      simp [hn],
    end }

/-- The image of a homomorphism `f : G â†’* H` is the subgroup of `H` whos carrier 
  is the image of `univ : set G`, i.e. `f '' univ` -/
def image (f : G â†’* H) : subgroup H :=
{ carrier := f '' univ, 
  one_mem' := âŸ¨1, mem_univ _, map_one _âŸ©,
  mul_mem' := 
    begin
      rintros _ _ âŸ¨x, hxâ‚€, hxâ‚âŸ© âŸ¨y, hyâ‚€, hyâ‚âŸ©,
      refine âŸ¨x * y, mem_univ _, _âŸ©,
      rw [map_mul f x y, hxâ‚, hyâ‚]
    end, 
  inv_mem' := 
    begin
      rintros _ âŸ¨x, hxâ‚€, hxâ‚âŸ©,
      refine âŸ¨xâ»Â¹, mem_univ _, _âŸ©,
      rw [â†hxâ‚, map_inv]
    end }

variables {f : G â†’* H}

lemma mem_kernel {g : G} : g âˆˆ kernel f â†” f g = 1 := 
  begin 
    split, intro h,
    apply mem_singleton_iff.1,
    apply mem_preimage.1,
    exact h,
    intro h,
    apply mem_singleton_iff.1,
    apply mem_singleton_iff.2, exact h,
  end
 -- âŸ¨Î» h, mem_singleton_iff.1 $ mem_preimage.1 h, Î» h, hâŸ©

lemma mem_kernel_of_eq {f : G â†’* H} {a b : G} (h : f a = f b) : 
  bâ»Â¹ * a âˆˆ kernel f := 
begin
  rw [â† group.mul_left_cancel_iff (f bâ»Â¹), â† map_mul] at h,
  symmetry' at h, 
  rw â† map_mul at h, 
  rw [group.mul_left_inv, map_one] at h,
  apply mem_singleton_iff.1,
  apply mem_preimage.1, 
  apply mem_singleton_iff.2,
  symmetry, exact h,
--original proof
  /-rw [mem_kernel, map_mul, map_inv, 
    â†group.mul_left_cancel_iff (f b)],
  simp [â†mul_assoc, h]-/
end
    
lemma mem_image {h : H} : h âˆˆ image f â†” âˆƒ g, f g = h := 
begin 
  refine âŸ¨Î» himg, _, Î» himg, _âŸ©,
    { rcases (mem_image f univ h).1 himg with âŸ¨g, _, hgâŸ©,
      exact âŸ¨g, hgâŸ© },
    { rcases himg with âŸ¨g, hgâŸ©,
      show h âˆˆ f '' univ,
      refine (mem_image f _ _).2 âŸ¨g, mem_univ g, hgâŸ© }
end

attribute [simp] mem_kernel mem_image

-- We will prove the classic results about injective homomorphisms that a 
-- homomorphism is injective if and only if it have the trivial kernel

open function

/-- A homomorphism `f` is injective iff. `f` has kernel `{1}` -/
theorem injective_iff_ker_eq_one : 
  injective f â†” (kernel f : set G) = {1} :=
begin -- Should we split this up into maby smaller proofs or should we keep it?

 --Was trying to rewrite proof differently, need to ask some things
/-split; intro hf,
  unfold injective at hf,
  have h : âˆ€ {a b : G},  f a = f b â†’ (bâ»Â¹*a) âˆˆ kernel f,
    { intros a b h1,
      apply mem_kernel_of_eq,
      exact h1,
    }, -/
  
  split; intro hf,
    { show f â»Â¹' {1} = {1},
      ext, split; intro hx,
        { apply @hf x 1,
          rw map_one, 
          exact mem_singleton_iff.1 hx },
        { rw [mem_preimage, mem_singleton_iff, mem_singleton_iff.1 hx],
          exact map_one _ } },
    { change f â»Â¹' {1} = {1} at hf,
      by_contra h, push_neg at h,
      rcases h with âŸ¨x, y, heq, hneqâŸ©,
      refine hneq (group.mul_right_cancel yâ»Â¹ _ _ _),
      have : x * yâ»Â¹ âˆˆ f â»Â¹' {1},
        apply group.mul_right_cancel (f y),
        rw [map_mul f, group.mul_assoc, map_inv, group.mul_left_inv, 
            group.one_mul, group.mul_one, heq],
      rw [hf, mem_singleton_iff] at this, 
      rw [this, group.mul_right_inv] }
end

/-- A homomorphism `f : G â†’* H` is surjective iff. the image of `f` is `H` -/
theorem surjective_iff_max_img : 
  surjective f â†” (image f : set H) = univ :=
begin
  split; intro hf,
    { ext y, split; intro hy,
        exact mem_univ _,
        cases hf y with x hx,
        refine âŸ¨x, mem_univ _, hxâŸ© },
    { intro y,
      rcases (show y âˆˆ (image f : set H), 
        by rw hf; exact mem_univ _) with âŸ¨x, _, hxâŸ©,
      exact âŸ¨x, hxâŸ© }
end

end group_hom

-- pushforward and pullback of subgroups

namespace subgroup

open set

variables {G : Type} [group G] {H : Type} [group H]

/-- image of a subgroup is a subgroup -/
def map (f : G â†’* H) (K : subgroup G) : subgroup H :=
{ carrier := f '' (K.carrier),
  one_mem' := begin
    rw mem_image,
    use 1,
    split,
    { exact K.one_mem},
    { exact f.map_one}
  end,
  mul_mem' := begin
    rintros _ _ âŸ¨a, ha, rflâŸ© âŸ¨b, hb, rflâŸ©,
    refine âŸ¨a*b, K.mul_mem ha hb, f.map_mul _ _âŸ©,
  end,
  inv_mem' := begin
    rintros _ âŸ¨a, ha, rflâŸ©,
    refine âŸ¨aâ»Â¹, K.inv_mem ha, f.map_invâŸ©,
  end }

lemma mem_map (f : G â†’* H) (K : subgroup G) (a : H) :
  a âˆˆ K.map f â†” âˆƒ g : G, g âˆˆ K âˆ§ f g = a := iff.rfl

end subgroup

namespace quotient

open group_hom lagrange mygroup.quotient function

variables {G H K : Type} [group G] [group H] [group K]

variable {f : G â†’* H}

/-- The natural map from a group `G` to its quotient `G / N` is a homomorphism -/
def mk (N : normal G) : G â†’* G /â‚˜ N := 
{ to_fun := Î» g, g,
  map_mul' := Î» _ _, by apply quotient.sound; refl }

variable {N : normal G}

/-- The natural homomorphism from a group `G` to its quotient `G / N` is a 
  surjection -/
theorem is_surjective : surjective $ mk N := exists_mk

-- The first isomorphism theorem states that for all `f : G â†’* H`, 
-- `G /â‚˜ kernel f â‰… image f`, we will prove this here.

-- We will use `lift_on` which is the same as `quotient.lift_on'`
-- Given an equivalence relation `R` on some type `Î±`, and a function `f : Î± â†’ Î²`
-- `Î» (a : quotient R), quotient.lift_on' a f h` is the function that maps `âŸ¦xâŸ§` 
-- to `f x` where `h` is a proof that this function is well defined, i.e. 
-- `âˆ€ x y : Î±, âŸ¦xâŸ§ = âŸ¦yâŸ§ â†’ f x = f y`.

-- First we prove such a map is well defined 
lemma map_of_lcoset_eq {f : G â†’* H} {x y : G} 
  (hxy : x â€¢ kernel f = y â€¢ kernel f) : f x = f y :=
begin
  rw [â†group.mul_left_cancel_iff (f y)â»Â¹, 
      â†map_inv, â†map_mul, map_inv, group.mul_left_inv,
      â†mem_kernel],
  exact lcoset_eq.1 hxy
end 

/-- Given a group homomorphism `f : G â†’* H`, `kernel_lift f` is a mapping from 
  the quotient `G /â‚˜ kernel f` to `H` such that `x â€¢ kernel f â†¦ f x` -/
def kernel_lift (f : G â†’* H) (x : G /â‚˜ kernel f) := lift_on x f $ 
  Î» _ _, map_of_lcoset_eq

@[simp] lemma kernel_lift_mk {f : G â†’* H} (g : G) :
  kernel_lift f (g : G /â‚˜ kernel f) = f g := rfl

/-- Given a group homomorphism `f : G â†’* H`, `kernel_lift f` form a homomorphism 
  `kernel_lift_hom f` from `(G /â‚˜ kernel f) â†’* H` -/
def kernel_lift_hom (f : G â†’* H) : (G /â‚˜ kernel f) â†’* H := 
{ to_fun := kernel_lift f,
  map_mul' := Î» x y, quotient.induction_onâ‚‚' x y $ Î» _ _,
    by simp only [quotient.coe, quotient.coe_mul, kernel_lift_mk, map_mul] }

/-- `kernel_lift_hom f` is an injective homomorphism -/
lemma injective_of_kernel_lift_hom {f : G â†’* H} : 
  injective $ kernel_lift_hom f :=
begin
  intros x y,
  apply quotient.induction_onâ‚‚' x y,
  intros _ _ hab,
  change kernel_lift f aâ‚ = kernel_lift f aâ‚‚ at hab,
  iterate 2 { rw kernel_lift_mk at hab, rw quotient.coe },
  rw [mk_eq, lcoset_eq],
  exact mem_kernel_of_eq hab 
end

/-- An alternative to `kernel_lift_hom` in which the range is restricted to the 
  image of `f` so that the homomorphism is also surjective -/
def kernel_lift_hom' (f : G â†’* H) : (G /â‚˜ kernel f) â†’* f.image :=
{ to_fun := Î» q, âŸ¨kernel_lift f q, quotient.induction_on' q $ 
    Î» a, show f a âˆˆ f.image, by exact âŸ¨a, mem_univ _, rflâŸ©âŸ©,
  map_mul' := Î» x y, quotient.induction_onâ‚‚' x y $ Î» _ _,
    by simpa only [quotient.coe, quotient.coe_mul, kernel_lift_mk, map_mul] }

lemma kernel_lift_hom'_eq_kernel_lift_hom {f : G â†’* H} (q : G /â‚˜ kernel f) : 
  (kernel_lift_hom' f q : H) = kernel_lift_hom f q := rfl

lemma injective_of_kernel_lift_hom' {f : G â†’* H} : 
  injective $ kernel_lift_hom' f := Î» x y hxy, injective_of_kernel_lift_hom $
by iterate 2 { rw â†kernel_lift_hom'_eq_kernel_lift_hom }; rw hxy

lemma surjective_of_kernel_lift_hom' {f : G â†’* H} : 
  surjective $ kernel_lift_hom' f := Î» âŸ¨_, x, _, rflâŸ©, âŸ¨x, kernel_lift_mk xâŸ©

/-- Given a group homomorphism `f`, `kernel_lift_hom' f` is a bijection -/
lemma bijective_of_kernel_lift_hom' {f : G â†’* H} :
  bijective $ kernel_lift_hom' f := 
âŸ¨injective_of_kernel_lift_hom', surjective_of_kernel_lift_hom'âŸ©

/-- The first isomorphism theorem: `G /â‚˜ kernel f â‰… image f` for `f : G â†’* H` 
  a group homomorphism -/
def quotient_kernel_iso_image (f : G â†’* H) : 
  G /â‚˜ kernel f â‰… image f := 
{ is_bijective := bijective_of_kernel_lift_hom',
  .. kernel_lift_hom' f }

-- Inclusion map as a homomorphism from a subgroup to the group
def ğ’¾ (H : subgroup G) : H â†’* G := 
{ to_fun := Î» h, (h : G),
  map_mul' := Î» _ _, rfl } 

@[simp] lemma ğ’¾_def {H : subgroup G} {h} (hh : h âˆˆ H) : ğ’¾ H âŸ¨h, hhâŸ© = h := rfl

-- The inclusion map is injective 
lemma injective_ğ’¾ {H : subgroup G} : injective $ ğ’¾ H := Î» _ _ hxy, subtype.eq hxy

-- The image of a surjective homomorphism is isomorphic to the group its mapped to
def iso_of_surjective {f : G â†’* H} (hf : surjective f) : image f â‰… H :=
{ is_bijective := âŸ¨ injective_ğ’¾, Î» y,
    âŸ¨âŸ¨y, show y âˆˆ f '' univ, by simp [hf y]âŸ©, by simp [ğ’¾]âŸ© âŸ©,
  .. ğ’¾ $ image f }

end quotient

namespace normal

open group_hom function mygroup.quotient

variables {G H : Type} [group G] [group H]

/-- The preimage of a normal subgroup is normal -/
def comap (f : G â†’* H) (N : normal H) : normal G := 
{carrier := fâ»Â¹' N,
 one_mem' := 
    begin
      rw [mem_preimage, map_one],
      exact N.one_mem', 
    end,
 mul_mem' :=
    begin
      intros x y h1 h2,
      rw mem_preimage at *,
      rw map_mul,
      exact N.mul_mem' h1 h2,
    end,
 inv_mem' := 
    begin
      intros x h,
      rw [mem_preimage] at *,
      rw map_inv,
      exact N.inv_mem' h,
    end,
 conj_mem' := 
    begin
      intros n h t ,
      rw [mem_preimage, map_mul, map_mul],
      rw mem_preimage at h,
      show _ âˆˆ N,
      convert N.conj_mem (f n) h (f t), 
      apply f.map_inv
    end }

/-- The surjective image of a normal subgroup is normal -/
def nmap {f : G â†’* H} (hf : surjective f) (N : normal G) : normal H := 
{ carrier := f '' N,
  one_mem' := âŸ¨1, N.to_subgroup.one_mem, f.map_oneâŸ©,
  mul_mem' := 
    begin
      rintros _ _ âŸ¨a, ha, rflâŸ© âŸ¨b, hb, rflâŸ©,
      refine âŸ¨a * b, N.to_subgroup.mul_mem ha hb, f.map_mul a bâŸ©,
    end,
  inv_mem' := 
    begin
      rintros _ âŸ¨a, ha, rflâŸ©,
      refine âŸ¨aâ»Â¹, N.to_subgroup.inv_mem ha, f.map_invâŸ©,    
    end,
  conj_mem' := 
    begin
      rintro _ âŸ¨b, hb, rflâŸ©,
      intro h,
      dsimp,
      rcases hf h with âŸ¨g, rflâŸ©,
      use g * b * gâ»Â¹,
      split,
      { exact N.conj_mem b hb g},
      { simp [f.map_mul] }
    end }

/-- Intersection of T and N is the pushforward to G of (the pullback to T of N) 
along the natural map T â†’ G -/
theorem subgroup_inf (N : normal G) (T : subgroup G) : 
  (T âŠ“ N) = subgroup.map (ğ’¾ T) (comap (ğ’¾ T) N) :=
begin
  ext x,
  split,
  { intro h,
    rw mem_inf at h,
    rw subgroup.mem_map,
    cases h with hxt hxn,
    use âŸ¨x, hxtâŸ©,
    split,
    { show _ âˆˆ â‡‘(ğ’¾ T) â»Â¹' â†‘N,
      exact hxn },
    { refl } },
  { rintro âŸ¨âŸ¨g, hgtâŸ©, ht1, rflâŸ©,
    change g âˆˆ T at hgt,
    rw mem_inf,
    split,
    { exact hgt },
    { exact ht1 } }
end

end normal

namespace quotient

variables {G H : Type} [group G] [group H]

def second_iso_theorem (T : subgroup G) (N : normal G) : 
  T /â‚˜ (N.comap (ğ’¾ T)) â‰… â†¥(T âŠ” N) /â‚˜ N.comap (ğ’¾ (T âŠ” N)) :=
{ to_fun := sorry,
  map_mul' := sorry,
  is_bijective := sorry }

-- to state this one we need to be able to push forward (`map`) a normal
-- subgroup along a surjection

def third_iso_theorem (T : normal G) (N : normal G)
  (h : T.carrier âŠ† N.carrier) :
  let NmodT : normal (G /â‚˜ T) := N.nmap (quotient.is_surjective) in  
   (G /â‚˜ T) /â‚˜ NmodT â‰… G /â‚˜ N := sorry

end quotient

end mygroup
