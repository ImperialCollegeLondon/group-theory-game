import subgroup.lagrange

open_locale classical

/-
  The type of group homs G â†’ H is
  denoted f : G â†’* H
  and the underlying function of types is â‡‘f : G â†’ H
  TODO: can we switch those arrows off?
  The axiom: if `f : G â†’* H` then `f.map_mul`
  is the assertion that for all a, b âˆˆ G
  we have f(a) = f(b).
  Our first job is to prove `f.map_one` and `f.map_inv`.

  `f.map_one : f 1 = 1`
  `f.map_inv {x : G} : f (xâ»Â¹) = f(x)â»Â¹`

-/

-- the entire project takes place in the mygroup namespace
namespace mygroup

open group_hom lagrange mygroup.quotient normal
  function set mygroup.subgroup

namespace quotient

variables {G H K : Type} [group G] [group H] [group K]
variables {f : G â†’* H}

/-- The natural map from a group `G` to its quotient `G / N` is a homomorphism -/
def mk (N : normal G) : G â†’* G /â‚˜ N :=
{ to_fun := Î» g, g,
  map_mul' := Î» _ _, by apply quotient.sound; refl }

variable {N : normal G}

-- The coercion from `G` to `G /â‚˜ N` is exactly our `mk` group hom
@[simp] lemma coe_eq_mk (g : G) : (g : G /â‚˜ N) = mk N g := rfl

-- The kernel of `mk N` is `N` 
lemma kernel_mk {N : normal G} : kernel (mk N) = N :=
begin
  ext x,
  show (x : G /â‚˜ N) = (1 : G) â†” x âˆˆ N.to_subgroup,
  rw [mk_eq, â†con_one_iff_mem],
  refl,
end

/-- The natural homomorphism from a group `G` to its quotient `G / N` is a
  surjection -/
theorem is_surjective_mk : surjective $ mk N := exists_mk

-- The first isomorphism theorem states that for all `f : G â†’* H`,
-- `G /â‚˜ kernel f â‰… image f`, we will prove this here.

-- We will use `lift_on` which is the same as `quotient.lift_on'`
-- Given an equivalence relation `R` on some type `Î±`, and a function `f : Î± â†’ Î²`
-- `Î» (a : quotient R), quotient.lift_on' a f h` is the function that maps `âŸ¦xâŸ§`
-- to `f x` where `h` is a proof that this function is well defined, i.e.
-- `âˆ€ x y : Î±, âŸ¦xâŸ§ = âŸ¦yâŸ§ â†’ f x = f y`.

/- Given a group hom `f : G â†’* H` and a normal subgroup `N` of `G`
with `N âŠ† kernel f`, there's an induced hom `G /â‚˜ N â†’ H` -/
def lift (f : G â†’* H) (N : normal G) (h : N.to_subgroup â‰¤ kernel f) :
G /â‚˜ N â†’* H :=
{ to_fun := Î» x, lift_on x f 
  begin
    intros x y h1,
    rw [con_of_normal_def, lcoset_rel_def, lcoset_eq] at h1,
    specialize h h1,
    change yâ»Â¹ * x âˆˆ f.kernel at h,
    rw mem_kernel at h,
    rw [map_mul, map_inv] at h,
    apply group.mul_left_cancel (f y)â»Â¹,
    rw h, simp
  end,
  map_mul' := Î» a b, quotient.induction_onâ‚‚' a b f.map_mul }

@[simp] lemma lift_mk {f : G â†’* H} (N : normal G) (h : N.to_subgroup â‰¤ kernel f)
  (g : G) : lift f N h (mk N g) = f g := rfl

lemma lift_image {f : G â†’* H} {N : normal G} (h : N.to_subgroup â‰¤ kernel f) :
  (lift f N h).image = f.image :=
begin
  ext x, split,
  { rintro âŸ¨a, rflâŸ©,
    rcases exists_mk a with âŸ¨g, rflâŸ©,
    exact group_hom.mem_image.2 âŸ¨g, rflâŸ© },
  { rintro âŸ¨a, rflâŸ©,
    exact âŸ¨(mk N a), rflâŸ© }
end

-- For the first isomorphism theorem, we will define a specific version of the
-- `lift` homomorphism above with `N = kernel f` 

-- First we prove such a map is well defined
lemma map_of_lcoset_eq {f : G â†’* H} {x y : G}
  (hxy : x â‹† kernel f = y â‹† kernel f) : f x = f y :=
begin
  rw [â†group.mul_left_cancel_iff (f y)â»Â¹,
      â†map_inv, â†map_mul, map_inv, group.mul_left_inv,
      â†mem_kernel],
  exact lcoset_eq.1 hxy
end

/-- Given a group homomorphism `f : G â†’* H`, `kernel_lift f` is a mapping from
  the quotient `G /â‚˜ kernel f` to `H` such that `x â‹† kernel f â†¦ f x` -/
def kernel_lift (f : G â†’* H) (x : G /â‚˜ kernel f) := lift_on x f $
  Î» _ _, map_of_lcoset_eq

@[simp] lemma kernel_lift_mk {f : G â†’* H} (g : G) :
  kernel_lift f (g : G /â‚˜ kernel f) = f g := rfl

/-- Given a group homomorphism `f : G â†’* H`, `kernel_lift f` form a homomorphism
  `kernel_lift_hom f` from `(G /â‚˜ kernel f) â†’* H` -/
def kernel_lift_hom (f : G â†’* H) : (G /â‚˜ kernel f) â†’* H :=
{ to_fun := kernel_lift f,
  map_mul' := Î» x y, quotient.induction_onâ‚‚' x y $ Î» _ _,
    by simp only [quotient.coe, quotient.coe_mul, kernel_lift_mk, map_mul] }

/-- `kernel_lift_hom f` is an injective homomorphism -/
lemma injective_of_kernel_lift_hom {f : G â†’* H} :
  injective $ kernel_lift_hom f :=
begin
  intros x y,
  apply quotient.induction_onâ‚‚' x y,
  intros _ _ hab,
  change kernel_lift f aâ‚ = kernel_lift f aâ‚‚ at hab,
  iterate 2 { rw kernel_lift_mk at hab, rw quotient.coe },
  rw [mk_eq, lcoset_eq],
  exact mem_kernel_of_eq hab
end

/-- An alternative to `kernel_lift_hom` in which the range is restricted to the
  image of `f` so that the homomorphism is also surjective -/
def kernel_lift_hom' (f : G â†’* H) : (G /â‚˜ kernel f) â†’* f.image :=
{ to_fun := Î» q, âŸ¨kernel_lift f q, quotient.induction_on' q $
    Î» a, show f a âˆˆ f.image, by exact âŸ¨a, rflâŸ©âŸ©,
  map_mul' := Î» x y, quotient.induction_onâ‚‚' x y $ Î» _ _,
    by simpa only [quotient.coe, quotient.coe_mul, kernel_lift_mk, map_mul] }

lemma kernel_lift_hom'_eq_kernel_lift_hom {f : G â†’* H} (q : G /â‚˜ kernel f) :
  (kernel_lift_hom' f q : H) = kernel_lift_hom f q := rfl

lemma injective_of_kernel_lift_hom' {f : G â†’* H} :
  injective $ kernel_lift_hom' f := Î» x y hxy, injective_of_kernel_lift_hom $
by iterate 2 { rw â†kernel_lift_hom'_eq_kernel_lift_hom }; rw hxy

lemma surjective_of_kernel_lift_hom' {f : G â†’* H} :
  surjective $ kernel_lift_hom' f := Î» âŸ¨_, x, rflâŸ©, âŸ¨x, kernel_lift_mk xâŸ©

/-- Given a group homomorphism `f`, `kernel_lift_hom' f` is a bijection -/
lemma bijective_of_kernel_lift_hom' {f : G â†’* H} :
  bijective $ kernel_lift_hom' f :=
âŸ¨injective_of_kernel_lift_hom', surjective_of_kernel_lift_hom'âŸ©

/-- The first isomorphism theorem: `G /â‚˜ kernel f â‰… image f` for `f : G â†’* H`
  a group homomorphism -/
def quotient_kernel_iso_image (f : G â†’* H) :
  G /â‚˜ kernel f â‰… image f :=
{ is_bijective := bijective_of_kernel_lift_hom',
  .. kernel_lift_hom' f }

-- The image of a surjective homomorphism is isomorphic to the group its mapped to
def iso_of_surjective {f : G â†’* H} (hf : surjective f) : image f â‰… H :=
{ is_bijective := âŸ¨ injective_ğ’¾, Î» y,
    âŸ¨âŸ¨y, show y âˆˆ range f, by simp [hf y]âŸ©, by simp [ğ’¾]âŸ© âŸ©,
  .. ğ’¾ $ image f }

/-- The first isomorphism theorem with a surjective homomorphism:
  `G /â‚˜ kernel f â‰… H` for `f : G â†’* H` a surjective group homomorphism-/
def quotient_kernel_iso_of_surjective {f : G â†’* H} (hf : surjective f) :
  G /â‚˜ kernel f â‰… H :=
iso_comp (quotient_kernel_iso_image f) $ iso_of_surjective hf

def subst_iso {A B : normal G} (h : A = B) : G /â‚˜ A â‰… G /â‚˜ B :=
{ is_bijective := begin subst h, convert bijective_id, ext x, rcases x, refl end,
  ..(lift (mk B) A (by {rw [h, kernel_mk], refl'})) }

def subst_iso' {A B : normal G} (h : A = B) (f : G /â‚˜ A â‰… H) : G /â‚˜ B â‰… H := 
iso_comp (subst_iso h.symm) f

def quotient_kernel_iso_of_surjective' {f : G â†’* H} (hf : surjective f) 
{N : normal G} (hN : kernel f = N) :
  G /â‚˜ N â‰… H := subst_iso' hN $ quotient_kernel_iso_of_surjective hf

/-- If `N âŠ† kernel f` then the kernel of induced map `lift f N h` is 
  image of `kernel f` -/
lemma lift_kernel {f : G â†’* H} {N : normal G} (h : N.to_subgroup â‰¤ kernel f) :
  kernel (lift f N h) = nmap is_surjective_mk (kernel f) :=
begin
  ext x, split,
  { intro h2, rcases quotient.exists_mk x with âŸ¨g, rflâŸ©,
    rw coe_eq_mk at *, rw [mem_kernel, lift_mk] at h2,
    refine âŸ¨g, mem_kernel.2 h2, rflâŸ© },
  { rintro âŸ¨g, hg1, rflâŸ©,
    change g âˆˆ f.kernel at hg1,
    rw mem_kernel at *, rw lift_mk, assumption }
end

/-
  The way to think about the formulation of the second isomorphism theorem is
  think about the individual mappings.

  First let us think about what `comap (ğ’¾ T) N` represents.
  `comap f N` is the normal subgroup whose underlying set is the preimage of `N`
  alongside `f`, so `comap (ğ’¾ T) N` is the normal subgroup of `T` that gets
  mapped into `N` under the inclusion map, i.e. elements in `T âˆ© N`.

  Now let us consider `comap (ğ’¾ (T âŠ” N)) N` which is similar.

  Again the the `comap (ğ’¾ (T âŠ” N)) N` is the preimage along the inclusion map
  `ğ’¾ (T âŠ” N)`. But this this time we see that `N âŠ† T âŠ” N`so their intersection
  are is just `N`.

  So why are we going through this trouble just to get back to `N`? The reason
  is that we have defined quotients to be an operation on a group and its normal
  subgroups. Notice that `comap (ğ’¾ (T âŠ” N)) N` does not have the same type as `N`.
  While `N` has type `normal G`, `comap (ğ’¾ (T âŠ” N)) N` has type `normal (T âŠ” N)`
  as demonstrated by the diagramme above. This is the type we need since we
  can only quotient `T âŠ” N` by one of its normal subgroups.
-/

-- Proving the second isomorphism theorem using the first 

-- `aux_hom` is the natural group homomorphism that maps `t : T` to 
-- `(t : T â¨¯ N) : (T â¨¯ N) /â‚˜ comap (ğ’¾ (T â¨¯ N)) N`
def aux_hom (T : subgroup G) (N : normal G) : 
  T â†’* â†¥(T â¨¯ N) /â‚˜ comap (ğ’¾ (T â¨¯ N)) N := (mk _) âˆ˜* (to_prod T N)

@[simp] lemma aux_hom_def {T : subgroup G} {N : normal G} (g) : 
  aux_hom T N g = (mk _) (to_prod T N g) := rfl

-- `aux_hom` has kernel `comap (ğ’¾ T) N` or equivalently `T âŠ“ N`
lemma aux_hom_kernel {T : subgroup G} {N : normal G} : 
  kernel (aux_hom T N) = comap (ğ’¾ T) N :=
begin
  ext, split; 
    { rw [mem_kernel, aux_hom_def, â†coe_eq_mk, â†coe_one, mk_eq'],
      intro hx, simpa using hx }
end

-- `aux_hom` is a surjective homomorphism
lemma aux_hom_surjective (T : subgroup G) (N : normal G) : 
  surjective $ aux_hom T N :=
begin
  intro y,
  rcases exists_mk y with âŸ¨g, rflâŸ©,
  rcases g with âŸ¨_, t, ht, n, hn, rflâŸ©,
  refine âŸ¨âŸ¨t, htâŸ©, _âŸ©,
  rw [aux_hom_def, â†coe_eq_mk, mk_eq'],
  simp [group.mul_assoc, show nâ»Â¹ âˆˆ N, by exact inv_mem (N : subgroup G) hn]
end

def second_iso_theorem (T : subgroup G) (N : normal G) :
  T /â‚˜ comap (ğ’¾ T) N â‰… â†¥(T â¨¯ N) /â‚˜ comap (ğ’¾ (T â¨¯ N)) N := 
quotient_kernel_iso_of_surjective' (aux_hom_surjective T N) aux_hom_kernel

-- To state the third isomorphism theorem we need to be able to push forward 
-- (`map`) a normal subgroup along a surjection

def third_iso_theorem (T : normal G) (N : normal G) (h : T.to_subgroup â‰¤ N) :
  let NmodT : normal (G /â‚˜ T) := N.nmap is_surjective_mk in
   (G /â‚˜ T) /â‚˜ NmodT â‰… G /â‚˜ N :=
let f : G /â‚˜ T â†’* G /â‚˜ N := (lift (mk N) _ (by { convert h, rw kernel_mk })) in 
iso_comp (subst_iso $ 
    show nmap is_surjective_mk N = f.kernel, by rw [lift_kernel, kernel_mk]) $
  quotient_kernel_iso_of_surjective 
    (by { rw [surjective_iff_max_img, lift_image, â†surjective_iff_max_img],
      exact is_surjective_mk })

-- We would like to define `correspondence N : H â†¦ H /â‚˜ N` so first we need to 
-- show if `H : subgroup G` with `(N : subgroup G) â‰¤ H`, then `â†‘N` is a normal 
-- subgroup of `H`

-- I think what I need it a way to make a subgroup from a group, i.e. given `G` 
-- and `H` both groups, if there is a map from `H` to `G`, `f` then there 
-- is a induced subgroup of `G` from `H` using `image f`

/-- `qmap` is a group homomorphism from `H /â‚˜ N â†’* G /â‚˜ N` where `H : subgroup G` 
  and `N : normal G` such that `h â‹† N â†¦ h â‹† N` -/ 
def qmap (H : subgroup G) (N : normal G) : H /â‚˜ comap (ğ’¾ H) N â†’* G /â‚˜ N :=
  let Ï† : H â†’* G /â‚˜ N := âŸ¨Î» h, mk N h.1, Î» _ _, rflâŸ© in lift Ï† (comap (ğ’¾ H) N) $ 
begin
  rintro âŸ¨x, hxâ‚€âŸ© hx,
  show _ âˆˆ kernel Ï†,
  rw mem_kernel,
  show mk N x = 1,
  rw [â†coe_eq_mk, â†coe_one, mk_eq'],
  change _ âˆˆ normal.comap (ğ’¾ H) N at hx,
  simp * at *,
end

@[simp] lemma qmap_eq {H : subgroup G} {N : normal G} (h : H) : 
  qmap H N h = mk N h := rfl

lemma mem_qmap_image {H : subgroup G} {N : normal G} (h) : 
  h âˆˆ (qmap H N).image â†” âˆƒ (g : H /â‚˜ comap (ğ’¾ H) N), qmap H N g = h := iff.rfl

lemma injective_qmap {H : subgroup G} {N : normal G} : injective $ qmap H N :=
begin
  intros x y hxy,
  rcases exists_mk x with âŸ¨x, rflâŸ©,
  rcases exists_mk y with âŸ¨y, rflâŸ©,
  change mk N x = mk N y at hxy,
  rw [â†coe_eq_mk, â†coe_eq_mk, mk_eq'] at hxy,
  rw [mk_eq', mem_comap],
  exact hxy 
end

def correspondence (N : normal G) : 
  subgroup_ge G N â†’ subgroup_ge (G /â‚˜ N) âŠ¥ := Î» âŸ¨H, hHâŸ©,
subgroup_ge.mk (image $ qmap H N) bot_le

def correspondence_inv (N : normal G) : 
  subgroup_ge (G /â‚˜ N) âŠ¥ â†’ subgroup_ge G N := Î» âŸ¨H, hHâŸ©,
subgroup_ge.mk (subgroup.comap (mk N) H) $
  Î» n hn, show mk N n âˆˆ H, 
  by { convert one_mem H, rw [â†mem_kernel, kernel_mk], exact hn }

lemma correspondence_left_inv (N : normal G) : 
  left_inverse (correspondence_inv N) (correspondence N) :=
begin
  rintro âŸ¨H, hHâŸ©,
  suffices : comap (mk N) (qmap H N).image = H,
   { simpa [correspondence, correspondence_inv] },
  ext x, split; intro hx,
   { cases hx with x' hx', rcases exists_mk x' with âŸ¨g, rflâŸ©,
     rw [qmap_eq, â†coe_eq_mk, â†coe_eq_mk, mk_eq'] at hx',
     rw â†group.inv_inv x,
     refine inv_mem H _,
     rw [â†group.mul_one xâ»Â¹, â†group.mul_right_inv (g : G), â†group.mul_assoc],
     exact mul_mem H (hH hx') (inv_mem H g.2) },
   { refine âŸ¨((âŸ¨x, hxâŸ© : H) : H /â‚˜ comap (ğ’¾ H) N), _âŸ©,
     rw qmap_eq, refl }
end

lemma correspondence_right_inv (N : normal G) : 
  right_inverse (correspondence_inv N) (correspondence N) :=
begin
  rintro âŸ¨H, _âŸ©,
  suffices : (qmap (comap (mk N) H) N).image = H,
    { simpa [correspondence, correspondence_inv] },
  ext x, split; intro hx,
    { rcases hx with âŸ¨x', rflâŸ©,
      rcases exists_mk x' with âŸ¨âŸ¨g, hgâŸ©, rflâŸ©,
      exact hg },
    { rcases exists_mk x with âŸ¨g, rflâŸ©,
      exact âŸ¨(mk (comap (ğ’¾ (comap (mk N) H)) N)) âŸ¨g, hxâŸ©, rflâŸ© }
end

def subgroups_of_quotient_equiv (N : normal G) : 
  subgroup_ge G N â‰ƒ subgroup_ge (G /â‚˜ N) âŠ¥ := 
{ to_fun := correspondence N,
  inv_fun := correspondence_inv N,
  left_inv := correspondence_left_inv N,
  right_inv := correspondence_right_inv N }

def gc (N : normal G) : galois_connection (subgroups_of_quotient_equiv N).to_fun 
  (subgroups_of_quotient_equiv N).inv_fun := 
begin
  intros A B,
  cases A with A hA,
  cases B with B _,
  split; intros h a ha,
    { exact h âŸ¨((âŸ¨a, haâŸ© : A) : _), rflâŸ© },
    { rcases ha with âŸ¨x, rflâŸ©,
      rcases exists_mk x with âŸ¨âŸ¨g, hgâŸ©, rflâŸ©,
      exact h hg }
end

-- â†“ This turns out to be useless since gc + equiv = order_iso â‡’ gi...
def gi (N : normal G) : galois_insertion (subgroups_of_quotient_equiv N).to_fun 
  (subgroups_of_quotient_equiv N).inv_fun := 
{ choice := Î» x _, (subgroups_of_quotient_equiv N).to_fun x,
  gc := gc N,
  le_l_u := Î» x, by { rw (subgroups_of_quotient_equiv N).right_inv, exact le_refl _ },
  choice_eq := Î» _ _, rfl }

/-- The subgroups of `G` greater than some `N : normal G` is order isomorphic to 
  the subgroups of `G /â‚˜ N` greater than `âŠ¥` -/
def subgroups_of_quotient_order_iso (N : normal G) : 
  let A := subgroup_ge G N in 
  let B := subgroup_ge (G /â‚˜ N) âŠ¥ in
  ((â‰¤) : A â†’ A â†’ Prop) â‰ƒo ((â‰¤) : B â†’ B â†’ Prop) :=
lattice.order_iso_of_equiv_gi (subgroups_of_quotient_equiv N) (gc N)

/-- The subgroups of `G` greater than some `N : normal G` is order isomorphic to 
  the subgroups of `G /â‚˜ N` -/
def subgroups_of_quotient_order_iso' (N : normal G) : 
  let A := subgroup_ge G N in 
  let B := subgroup (G /â‚˜ N) in
  ((â‰¤) : A â†’ A â†’ Prop) â‰ƒo ((â‰¤) : B â†’ B â†’ Prop) :=
order_iso.trans (subgroups_of_quotient_order_iso N) $
  order_iso.symm ge.subgroup_ge_bot_order_iso

-- We would like a lemma that states : Given `G` groups, `N : normal G`, the 
-- preimage of `H : subgroup G /â‚˜ N` under the natural map from `G` to `G /â‚˜ N`
-- is a subgroup of `G` with order `|H||N|`.

def quotient.comap (N : normal G) (H : subgroup (G /â‚˜ N)) : subgroup G := 
  subgroup.comap (mk N) H

lemma quotient.comap_le (N : normal G) (H : subgroup (G /â‚˜ N)) : 
  (N : set G) âŠ† quotient.comap N H := Î» n hn, 
show _ âˆˆ H, by { convert one_mem H, rw [â† mem_kernel, kernel_mk], exact hn }

-- The way to think about this is that `mkâ»Â¹ H` is the new group with `N` its 
-- normal subgroup. Now, by `kernel_mk` we have `kernel mk = N` so by the 
-- first isomorphism theorem `mkâ»Â¹ H /â‚˜ N â‰… H`

lemma quotient.comap_iso (N : normal G) (H : subgroup (G /â‚˜ N)) :
  quotient.comap N H /â‚˜ comap (ğ’¾ $ quotient.comap N H) N â‰… H :=
  let f : quotient.comap N H â†’* H :=
    { to_fun := Î» x, âŸ¨mk N x.1, x.2âŸ©,
      map_mul' := Î» _ _, rfl } in
  quotient_kernel_iso_of_surjective' (begin
    rintro âŸ¨y, hyâŸ©,
    rcases exists_mk y with âŸ¨g, rflâŸ©,
    use [g, hy], refl
  end : surjective f)
  begin
    ext x,
    rw [mem_kernel, mem_comap],
    unfold coe_fn has_coe_to_fun.coe,
    simp only [to_fun_eq_coe, subtype.val_eq_coe],
    cases x with x hx,
    change _ â†” x âˆˆ N,
    conv_rhs begin rw â†@kernel_mk _ _ N, end,
    rw mem_kernel,
    show _ = (âŸ¨(1 : G /â‚˜ N), _âŸ© : H) â†” _, simp
  end

lemma quotient.comap_card_eq [fintype G] (N : normal G) (H : subgroup (G /â‚˜ N)) : 
  fincard (quotient.comap N H) = 
  fincard (normal.comap (ğ’¾ $ quotient.comap N H) N) * fincard H :=
begin
  rw [fincard.of_equiv (mul_equiv_of_iso (iso_symm (quotient.comap_iso N H))).to_equiv,
     â† lagrange.card_quotient_eq_mul]
end

end quotient

end mygroup
